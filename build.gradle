import groovy.swing.SwingBuilder

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'maven-publish'
    id "net.ltgt.apt" version "0.10"
}

ext {
    rxJavaVer = ' 2.2.5'
    retrofitVer = '2.5.0'
    okHttpVer = '3.12.1'
    gsonVer = '2.8.5'
    username = ''
    password = ''
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'org.apache.commons:commons-lang3:3.8.1'

    implementation 'com.google.guava:guava:27.0.1-jre'

    implementation "io.reactivex.rxjava2:rxjava:${rxJavaVer}"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:${retrofitVer}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofitVer}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVer}"

    implementation "com.google.code.gson:gson:${gsonVer}"

    implementation "com.google.dagger:dagger:2.17"
    annotationProcessor "com.google.dagger:dagger-compiler:2.17"

    // okHttp
    implementation "com.squareup.okhttp3:okhttp:${okHttpVer}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okHttpVer}"

    implementation group: 'commons-collections', name: 'commons-collections', version: '3.2'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.2'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.2'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenLocal()
    maven {
        name "YosemiteLabs Release"
        url "http://backoffice.yosemitelabs.org:8081/repository/maven-releases/"
    }
    maven {
        name "YosemiteLabs Snapshot"
        url "http://backoffice.yosemitelabs.org:8081/repository/maven-snapshots/"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

version = '0.9.1-SNAPSHOT'
group = 'io.yosemite'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

uploadArchives {
    repositories {
        mavenDeployer {
            gradle.taskGraph.whenReady { taskGraph ->
                if (taskGraph.hasTask(':uploadArchives')) {
                    def console = System.console()
                    if (console == null) {
                        throw new Exception("run gradle with --no-daemon")
                    }

                    console.printf("\nUsername: ")
                    console.flush()
                    def user = console.readLine()
                    console.printf("Password: ")
                    console.flush()
                    def pass = console.readPassword()
                    username = new String(user)
                    password = new String(pass)

                    if (username.size() <= 0) {
                        throw new Exception("You must enter a username to proceed.")
                    }
                    if (password.size() <= 0) {
                        throw new Exception("You must enter a password to proceed.")
                    }

                    repository(url: 'http://backoffice.yosemitelabs.org:8081/repository/maven-releases/') {
                        authentication(userName: username, password: password)
                    }
                    snapshotRepository(url: 'http://backoffice.yosemitelabs.org:8081/repository/maven-snapshots/') {
                        authentication(userName: username, password: password)
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'yosemitej'
            from components.java
            pom {
                name = 'Yosemite Java SDK'
                description = 'The project\'s goal would be something like Java version of clyos which provides convenient interfaces for the useful commands such as pushing action including some native actions of Yostemite Public Blockchain'
                url = 'http://yosemitex.com'
            }
        }
    }
}

sourceSets {
    sample {
        java {
            srcDir "src/sample/java"
        }
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    sampleCompile.extendsFrom testCompile
    sampleRuntime.extendsFrom testRuntime
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

test {
    testLogging {
        showStandardStreams true
        exceptionFormat 'full'
    }
}

task buildSample(type: JavaCompile, dependsOn: build) {
    source = fileTree(dir: 'src/sample', include: '**/*.java')
    classpath = files("$buildDir/classes/java/main")
    destinationDir = file("$buildDir/classes/java/main")
}

task runSample(type: JavaExec, dependsOn: buildSample) {
    if (project.hasProperty('pargs')) {
        args(pargs.split(','))
    }
    doFirst {
        main = 'io.yosemite.sample.' + mainClass
        classpath = sourceSets.main.runtimeClasspath
    }
}

build.finalizedBy buildSample
